class Area(Int areaId, Double latitude1, Double longitude1, Double latitude2, Double longitude2, Boolean isCriticalArea)
    Boolean hasPosition(MovableEntity movableEntity)
        Boolean inLatitudeRange = (movableEntity.latitude >= this.latitude1 & movableEntity.latitude <= this.latitude2);
        Boolean inLongitudeRange = (movableEntity.longitude >= this.longitude1 & movableEntity.longitude <= this.longitude2);

        if (inLatitudeRange & inLongitudeRange) then
            return True;
        end
        return False;
    end
end

class Room(Int roomId, String name, Wall wall, Area area) end

class Door() end

class MovableEntity(Int movableEntityId, Double latitude, Double longitude) models "domain:MovableEntity"; end

class Wall(Door door) end


main
    Door door = new Door();
    Wall wall = new Wall(door);

    Double corner1Latitude = 59.9441196;
    Double corner1Longitude = 10.7193836;
    Double corner2Latitude = 59.9442128;
    Double corner2Longitude = 10.7194811;

    Area area = new Area(1, corner1Latitude, corner1Longitude, corner2Latitude, corner2Longitude, False);
    Area criticalArea = new Area(2, corner1Latitude, corner1Longitude, corner2Latitude, corner2Longitude, True);

    Room roomIFI = new Room(3, "IFI Study Room - Open", wall, area);
    Room criticalRoomIFI = new Room(3, "IFI Study Room - Closed", wall, criticalArea);

    List<Room> rooms = access("SELECT ?obj WHERE{ ?obj a prog:Room }");
    Int length = rooms.length();

    print("The rooms:");
    Int i = 0;
    while (i < length) do
        Room room = rooms.get(i);
        print(room.name);
        i = i + 1;
    end

    List<Double> coordinates = null;
    coordinates = access(
    "from(bucket: \"Data\")
        |> range(start: 0)
        |> filter(fn: (r) => r[\"_measurement\"] == \"data\")
        |> filter(fn: (r) => r[\"_field\"] == \"latitude\" or r[\"_field\"] == \"longitude\")
        |> sort(columns: [\"_time\"], desc: true)
        |> unique(column: \"id\")
        |> aggregateWindow(every: 5m, fn: first, createEmpty: false)
        |> yield(name: \"mean\")",
    INFLUXDB("influx.yml"));

    List<MovableEntity> objects = construct("SELECT * WHERE {?a a domain:MovableEntity . ?a domain:movableEntityId ?movableEntityId . ?a domain:latitude ?latitude . ?a domain:longitude ?longitude}");

    length = coordinates.length();
    Int half = length / 2;
    i = 0;

    print("---");
    print("The smartphone(s) as instance(s) of domain:MovableEntity:");
    // BUG in SMOL? Doesn't stop at index 0 in array, prints value on index 0 for any index less than it, e.g while(j > -100)
    while (i < half) do
        Double lat = coordinates.get(half + i);
        Double long = coordinates.get(i);

        Int index = half - i;
        MovableEntity smartphone = objects.get(index - 1);
        smartphone.latitude = lat;
        smartphone.longitude = long;

        print(smartphone.movableEntityId);
        print(smartphone.latitude);
        print(smartphone.longitude);
        
        i = i + 1;
    end

    List<MovableEntity> smartphones = access("SELECT ?obj WHERE{ ?obj a prog:MovableEntity }");
    length = smartphones.length();

    print("The smartphone(s) as instance(s) of prog:MovableEntity:");
    i = 0;
    while (i < length) do
        MovableEntity m = smartphones.get(i);

        print(m.movableEntityId);
        print(m.latitude);
        print(m.longitude);

        i = i + 1;
    end

    print("---");
    print("Checking location of smartphone(s):");
    print("TODO: Check with SparQL query instead!");
    
    i = 0;
    // To add positions to asset model, can loop over smartphones from asset model gotten by SPARQL, then create smartphone objects in SMOL and add the positions gotten from InfluxDB
    // TODO: Breakpoint in SMOL and query in terminal to check if a smartphone is in a critial area
    while (i < length) do
        MovableEntity next = objects.get(i);
        print(next.movableEntityId);

        // Test case: 1
        // The smartphone should be inside the non-critical area (room) when it's open during the day
        // Else print False
        Boolean isInside = roomIFI.area.hasPosition(next);
        if (isInside & !roomIFI.area.isCriticalArea) then
            print("Smartphone is inside the safe area!");
        else
            print(False);
        end

        // Test case: 2
        //Smartphone should be outside the critical area (room) when it's closed at night
        // Else print False
        isInside = criticalRoomIFI.area.hasPosition(next);
        if (!isInside & criticalRoomIFI.area.isCriticalArea) then 
            print("Smartphone is outside the critical area!"); 
        else
            print(False);
        end
        
        i = i + 1;
    end

    // Accessing all MovableEntity objects (smartphones)
    List<MovableEntity> movableEntities = access("SELECT ?obj WHERE{ ?obj a prog:MovableEntity. }");

    print("Validating SHACL shapes..");
    Boolean validation = validate("output.ttl");

    print(validation);

end